import mlda.Base

inductive Three : Type where
  | false
  | byzantine
  | true

notation "ğŸ¯" => Three

namespace Three

namespace Atom

variable
  {X : Type}

@[simp]
def neg : ğŸ¯ â†’ ğŸ¯
  | .false => .true
  | .byzantine => .byzantine
  | .true => .false
scoped prefix:75 "Â¬" => neg

example : ğŸ¯ := Â¬ Three.false

@[simp]
def and : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯
  | .true, .true => .true
  | .byzantine, .true => .byzantine
  | .true, .byzantine => .byzantine
  | .byzantine, .byzantine => .byzantine
  | _, _ => .false

scoped infixl:35 " âˆ§ " => and

instance : Std.Associative and where
  assoc := by intro a b c; cases a <;> cases b <;> cases c <;> simp

instance : Std.Commutative and where
  comm := by intro a b; cases a <;> cases b <;> simp

instance : Std.LawfulCommIdentity and .true where
  left_id := by intro a; cases a <;> simp

@[simp]
def or : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯
  | .false, .false => .false
  | .false, .byzantine => .byzantine
  | .byzantine, .false => .byzantine
  | .byzantine, .byzantine => .byzantine
  | _, _ => .true

scoped infixl:30 " âˆ¨ " => or

instance : Std.Associative or where
  assoc := by intro a b c; cases a <;> cases b <;> cases c <;> simp

instance : Std.Commutative or where
  comm := by intro a b; cases a <;> cases b <;> simp

instance : Std.LawfulCommIdentity or .false where
  left_id := by intro a; cases a <;> simp

@[simp]
def xor : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯
  | .byzantine, _ => .byzantine
  | _, .byzantine => .byzantine
  | .true, .true => .false
  | .false, .false => .false
  | _, _ => .true

scoped infixl:30 " âŠ• " => xor

@[simp]
def impl (a b : ğŸ¯) : ğŸ¯ := (Â¬ a) âˆ¨ b

scoped infixl:25 " â†’ " => impl

@[simp]
def isTrue : ğŸ¯ â†’ ğŸ¯
 | .true => .true
 | _ => .false
scoped notation "T" => isTrue

@[simp]
def isByzantine : ğŸ¯ â†’ ğŸ¯
 | .byzantine => .true 
 | _ => .false
scoped notation "B" => isByzantine

@[simp]
def isFalse : ğŸ¯ â†’ ğŸ¯
 | .false => .true 
 | _ => .false
scoped notation "F" => isFalse

@[simp]
def isNotFalse : ğŸ¯ â†’ ğŸ¯
 | .false => .false 
 | _ => .true
scoped notation "TB" => isNotFalse

@[simp]
def isNotByzantine : ğŸ¯ â†’ ğŸ¯
 | .byzantine => .false 
 | _ => .true
scoped notation "TF" => isNotByzantine

@[simp]
def strongImpl (a b : ğŸ¯) : ğŸ¯ := a â†’ T b

scoped infixl:25 " â‡€ " => strongImpl

inductive Valid : ğŸ¯ â†’ Prop where
  | true : Valid .true
  | byzantine : Valid .byzantine
scoped notation "âŠ¨" => Valid

inductive NotValid : ğŸ¯ â†’ Prop where
  | false : NotValid .false
scoped notation "âŠ­" => NotValid

instance : Min ğŸ¯ where
  min := and

instance : Max ğŸ¯ where
  max := or

instance : Ord ğŸ¯ where
  compare := fun
   | .false, .false => .eq
   | .false, _ => .lt
   | _, .false => .gt
   | .byzantine, .byzantine => .eq
   | .byzantine, .true => .lt
   | .true, .byzantine => .gt
   | .true, .true => .eq

instance : LinearOrder Three := by
  let toFin : ğŸ¯ â†’ Fin 3
    | .false => 0
    | .byzantine => 1
    | .true => 2
  apply LinearOrder.liftWithOrd' toFin
  intro x y p; cases x <;> cases y <;> cases p <;> rfl
  intro x y; cases x <;> cases y <;> rfl

instance : BoundedOrder Three where
  bot := .false
  bot_le := by intro a; cases a <;> decide
  top := .true
  le_top := by intro a; cases a <;> decide

instance : DistribLattice Three where
  le_sup_inf := by intro a b c; cases a <;> cases b <;> cases c <;> decide

namespace Proposition_2_2_2

variable (a b : ğŸ¯)

@[simp] theorem p1_1 : âŠ¨ .true := .true
@[simp] theorem p1_2 : âŠ¨ .byzantine := .byzantine
@[simp] theorem p1_3 : âŠ­ .false := .false
@[simp] theorem p1_4 : Â¬ (âŠ¨ .false) := by intro k; cases k
@[simp] theorem p1_5 : Â¬ (âŠ­ .true) := by intro k; cases k
@[simp] theorem p1_6 : Â¬ (âŠ­ .byzantine) := by intro k; cases k

theorem p2_1 : âŠ¨ (a âˆ¨ b) â†” âŠ¨ a âˆ¨ âŠ¨ b := by
  constructor <;> intro x
  next => cases a <;> cases b <;> cases x <;> simp
  next => cases x <;> rename_i k <;> cases a <;> cases b <;> cases k <;> simp

theorem p2_2 : âŠ¨ (a âˆ§ b) â†” âŠ¨ a âˆ§ âŠ¨ b := by
  constructor <;> intro x
  next => cases a <;> cases b <;> cases x <;> simp
  next => rcases x with âŸ¨k1, k2âŸ©; cases a <;> cases b <;> cases k1 <;> cases k2 <;> simp

theorem p3_1 : (a â†’ b) = (Â¬ a âˆ¨ b) := by cases a <;> cases b <;> rfl
theorem p3_2 : (a â‡€ b) = (a â†’ T b) := by cases a <;> cases b <;> rfl

theorem p4 : âŠ¨ (a â†’ b) â†” ((a = .true) â†’ âŠ¨ (TB b)) := by
  constructor <;> cases a <;> cases b <;> simp

theorem p5 : âŠ¨ (a â‡€ b) â†” ((a = .true) â†’ (b = .true)) := by
  constructor <;> cases a <;> cases b <;> simp

theorem p6 : âŠ¨ (a âˆ¨ Â¬ a) := by cases a <;> simp

theorem p7 : âŠ¨ (a âˆ§ Â¬ a) â†” a = .byzantine := by
  constructor <;> cases a <;> simp

theorem p8 : âŠ¨ a â†” (TF a = T a) := by cases a <;> simp

theorem p9 : a â‰¤ b â†” ((Â¬ b) â‰¤ Â¬ a) := by
  constructor <;> cases a <;> cases b <;> decide

end Proposition_2_2_2

end Atom

namespace Function

variable {X : Type}

def bigAnd (f : X â†’ ğŸ¯) (l : Finset X) : ğŸ¯ := l.fold Atom.and .true f
scoped notation "â‹€" => bigAnd

def bigOr (f : X â†’ ğŸ¯) (l : Finset X) : ğŸ¯ := l.fold Atom.or .false f
scoped notation "â‹" => bigOr

@[simp] def lift1 (op : ğŸ¯ â†’ ğŸ¯) (f : X â†’ ğŸ¯) : X â†’ ğŸ¯ := op âˆ˜ f
@[simp] def lift2 (op : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯) (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := fun x => op (f x) (f' x)

@[simp] def neg (f : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift1 Atom.neg f
scoped prefix:75 "Â¬" => neg

@[simp] def and (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.and f f'
scoped infixl:35 " âˆ§ " => and

@[simp] def or (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.or f f'
scoped infixl:30 " âˆ¨ " => or

def impl (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.impl f f'
def strongImpl (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.strongImpl f f'

end Function
end Three
