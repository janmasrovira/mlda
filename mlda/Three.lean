import mlda.Base

inductive Three : Type where
  | false
  | byzantine
  | true

notation "ğŸ¯" => Three

namespace Three

namespace Atom

variable
  {X : Type}

def neg : ğŸ¯ â†’ ğŸ¯
  | false => true
  | byzantine => byzantine
  | true => false
scoped prefix:75 "Â¬" => neg

example : ğŸ¯ := Â¬ Three.false

def and : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯
  | true, true => true
  | byzantine, true => byzantine
  | true, byzantine => byzantine
  | byzantine, byzantine => byzantine
  | _, _ => false

scoped infixl:35 " âˆ§ " => and

instance : Std.Associative and where
  assoc := by intro a b c; cases a <;> cases b <;> cases c <;> simp!

instance : Std.Commutative and where
  comm := by intro a b; cases a <;> cases b <;> simp!

instance : Std.LawfulCommIdentity and true where
  left_id := by intro a; cases a <;> simp!

def or : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯
  | false, false => false
  | false, byzantine => byzantine
  | byzantine, false => byzantine
  | byzantine, byzantine => byzantine
  | _, _ => true

scoped infixl:30 " âˆ¨ " => or

instance : Std.Associative or where
  assoc := by intro a b c; cases a <;> cases b <;> cases c <;> simp!

instance : Std.Commutative or where
  comm := by intro a b; cases a <;> cases b <;> simp!

instance : Std.LawfulCommIdentity or false where
  left_id := by intro a; cases a <;> simp!

def xor : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯
  | byzantine, _ => byzantine
  | _, byzantine => byzantine
  | true, true => false
  | false, false => false
  | _, _ => true
scoped infixl:30 " âŠ• " => xor

abbrev impl (a b : ğŸ¯) : ğŸ¯ := (Â¬ a) âˆ¨ b
scoped infixl:25 " â†’ " => impl

def isTrue : ğŸ¯ â†’ ğŸ¯
 | true => true
 | _ => false
scoped notation "T" => isTrue

def isByzantine : ğŸ¯ â†’ ğŸ¯
 | byzantine => true 
 | _ => false
scoped notation "B" => isByzantine

def isFalse : ğŸ¯ â†’ ğŸ¯
 | false => true 
 | _ => false
scoped notation "F" => isFalse

def isNotFalse : ğŸ¯ â†’ ğŸ¯
 | false => false 
 | _ => true
scoped notation "TB" => isNotFalse

def isNotByzantine : ğŸ¯ â†’ ğŸ¯
 | byzantine => false 
 | _ => true
scoped notation "TF" => isNotByzantine

def strongImpl : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯
 | false, _ => true
 | byzantine, true => true
 | byzantine, _ => byzantine
 | true, true => true 
 | true, _ => false
scoped infixl:25 " â‡€ " => strongImpl

inductive Valid : ğŸ¯ â†’ Prop where
  | true : Valid true
  | byzantine : Valid byzantine
scoped notation "âŠ¨" => Valid

inductive NotValid : ğŸ¯ â†’ Prop where
  | false : NotValid false
scoped notation "âŠ­" => NotValid

instance : Ord ğŸ¯ where
  compare := fun
   | false, false => .eq
   | false, _ => .lt
   | _, false => .gt
   | byzantine, byzantine => .eq
   | byzantine, true => .lt
   | true, byzantine => .gt
   | true, true => .eq

instance : Max Three where
  max := or

instance : Min Three where
  min := and

instance : LinearOrder Three := by
  let toFin : ğŸ¯ â†’ Fin 3
    | false => 0
    | byzantine => 1
    | true => 2
  apply LinearOrder.liftWithOrd toFin
  intro x y p; cases x <;> cases y <;> cases p <;> rfl
  repeat (intro x y; cases x <;> cases y <;> rfl)

instance : BoundedOrder Three where
  bot := false
  bot_le := by intro a; cases a <;> decide
  top := true
  le_top := by intro a; cases a <;> decide

instance : DistribLattice Three where
  le_sup_inf := by intro a b c; cases a <;> cases b <;> cases c <;> decide

namespace Proposition_2_2_2

variable {a b : ğŸ¯}

@[simp] theorem p1_1 : âŠ¨ true := .true
@[simp] theorem p1_2 : âŠ¨ byzantine := .byzantine
@[simp] theorem p1_3 : âŠ­ false := .false
@[simp] theorem p1_4 : Â¬ (âŠ¨ false) := by intro k; cases k
@[simp] theorem p1_5 : Â¬ (âŠ­ true) := by intro k; cases k
@[simp] theorem p1_6 : Â¬ (âŠ­ byzantine) := by intro k; cases k

theorem p2_1 : âŠ¨ (a âˆ¨ b) â†” âŠ¨ a âˆ¨ âŠ¨ b := by
  constructor <;> intro x
  next => cases a <;> cases b <;> cases x <;> simp
  next => cases x <;> rename_i k <;> cases a <;> cases b <;> cases k <;> simp!

theorem p2_2 : âŠ¨ (a âˆ§ b) â†” âŠ¨ a âˆ§ âŠ¨ b := by
  constructor <;> intro x
  next => cases a <;> cases b <;> cases x <;> simp
  next => rcases x with âŸ¨k1, k2âŸ©; cases a <;> cases b <;> cases k1 <;> cases k2 <;> simp!

theorem p3_1 : (a â†’ b) = (Â¬ a âˆ¨ b) := by cases a <;> cases b <;> rfl
theorem p3_2 : (a â‡€ b) = (a â†’ T b) := by cases a <;> cases b <;> rfl

theorem p4 : âŠ¨ (a â†’ b) â†” ((a = true) â†’ âŠ¨ (TB b)) := by
  cases a <;> cases b <;> simp [impl, or, neg, isNotFalse]

theorem p5 : âŠ¨ (a â‡€ b) â†” ((a = true) â†’ (b = true)) := by
  cases a <;> cases b <;> simp [strongImpl]

theorem p6 : âŠ¨ (a âˆ¨ Â¬ a) := by cases a <;> simp!

theorem p7 : âŠ¨ (a âˆ§ Â¬ a) â†” a = byzantine := by
  constructor <;> cases a <;> simp!

theorem p8 : âŠ¨ a â†” (TF a = T a) := by cases a <;> simp!

theorem p9 : a â‰¤ b â†” ((Â¬ b) â‰¤ Â¬ a) := by
  constructor <;> cases a <;> cases b <;> decide

end Proposition_2_2_2

end Atom

namespace Function

variable {X : Type}

abbrev bigAnd (P : Finset X) (f : X â†’ ğŸ¯) : ğŸ¯ := P.fold Atom.and true f
scoped notation "â‹€" => bigAnd

def bigOr (P : Finset X) (f : X â†’ ğŸ¯) : ğŸ¯ := P.fold Atom.or false f
scoped notation "â‹" => bigOr

@[simp] def lift1 (op : ğŸ¯ â†’ ğŸ¯) (f : X â†’ ğŸ¯) : X â†’ ğŸ¯ := op âˆ˜ f
@[simp] def lift2 (op : ğŸ¯ â†’ ğŸ¯ â†’ ğŸ¯) (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := fun x => op (f x) (f' x)

def neg (f : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift1 Atom.neg f
scoped prefix:75 "Â¬" => neg

def isNotByzantine (f : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift1 Atom.isNotByzantine f
scoped notation "TF" => isNotByzantine

theorem neg_fold {f : X â†’ ğŸ¯} : (fun x => Atom.neg (f x)) = (Â¬ f) := by rfl

def and (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.and f f'
scoped infixl:35 " âˆ§ " => and

def or (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.or f f'
scoped infixl:30 " âˆ¨ " => or

def impl (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.impl f f'
def strongImpl (f f' : X â†’ ğŸ¯) : X â†’ ğŸ¯ := lift2 Atom.strongImpl f f'

end Function

namespace Lemmas

open scoped Three.Function
open Three.Function
open Three.Atom

variable
  {X : Type}
  {P : Finset X}
  {a b c : ğŸ¯}
  {f f' : X â†’ ğŸ¯}

@[simp] theorem T_true : T a = true â†” a = true := by cases a <;> decide

theorem false_or_byzantine_le (a : ğŸ¯) : (a = Three.false) âˆ¨ .byzantine â‰¤ a := by cases a <;> decide

theorem true_or_le_byzantine (a : ğŸ¯) : (a = Three.true) âˆ¨ a â‰¤ .byzantine := by cases a <;> decide

theorem neg_or : (Â¬ (a âˆ¨ b)) = (Â¬ a âˆ§ Â¬ b) := by
  cases a <;> cases b <;> simp!

theorem neg_and : (Â¬ (a âˆ§ b)) = (Â¬ a âˆ¨ Â¬ b) := by
  cases a <;> cases b <;> simp!

theorem Function.neg_and : (Â¬ (f âˆ§ f')) = (Â¬ f âˆ¨ Â¬ f') := by
  rw [Three.Function.and, Three.Function.or, Three.Function.neg]
  funext; apply Lemmas.neg_and

@[simp] theorem neg_neg : (Â¬ Â¬ a) = a := by
  cases a <;> rfl

@[simp] theorem Function.neg_neg : (Â¬ (Â¬ f)) = f := by
  unfold Three.Function.neg; simp; funext a; rw [Function.comp, Function.comp]
  cases h : f a <;> rfl

theorem le_and : c â‰¤ (a âˆ§ b) â†” (c â‰¤ a âˆ§ c â‰¤ b) := by
  cases a <;> cases b <;> cases c <;> decide

theorem and_le : (a âˆ§ b) â‰¤ c â†” (a â‰¤ c âˆ¨ b â‰¤ c) := by
  cases a <;> cases b <;> cases c <;> decide

theorem and_true : (a âˆ§ b) = Three.true â†” (a = true âˆ§ b = true) := by
  cases a <;> cases b <;> decide

theorem and_byzantine : (a âˆ§ b) = Three.byzantine â†” (a = byzantine âˆ§ byzantine â‰¤ b) âˆ¨ (b = byzantine âˆ§ byzantine â‰¤ a) := by
  cases a <;> cases b <;> decide

theorem byzantine_le_and : Three.byzantine â‰¤ (a âˆ§ b) â†” (byzantine â‰¤ a âˆ§ byzantine â‰¤ b) := by
  cases a <;> cases b <;> decide

theorem and_false : (a âˆ§ b) = Three.false â†” (a = false âˆ¨ b = false) := by
  cases a <;> cases b <;> decide

theorem or_true : (a âˆ¨ b) = Three.true â†” (a = true âˆ¨ b = true) := by
  cases a <;> cases b <;> decide

@[simp] theorem bot_le : false â‰¤ a â†” True := by
  cases a <;> decide

@[simp] theorem le_bot : a â‰¤ false â†” a = false := by
  cases a <;> decide

@[simp] theorem false_lt : false < a â†” byzantine â‰¤ a := by
  cases a <;> decide

@[simp] theorem lt_true : a < true â†” a â‰¤ byzantine := by
  cases a <;> decide

@[simp] theorem top_le : true â‰¤ a â†” a = true := by
  cases a <;> decide

@[simp] theorem le_top : a â‰¤ true â†” True := by
  cases a <;> decide

theorem byzantine_le : byzantine â‰¤ a â†” a = byzantine âˆ¨ a = true := by
  cases a <;> decide

theorem le_byzantine : a â‰¤ byzantine â†” a = false âˆ¨ a = byzantine := by
  cases a <;> decide

theorem le_helper (p : byzantine â‰¤ a â†’ b â‰¤ byzantine â†’ a â‰¤ b) : a â‰¤ b := by
  cases a <;> cases b <;> try decide
  repeat (simp at p)

theorem le_by_cases (c1 : a = true â†’ b â‰¤ byzantine â†’ b = true)
                    (c2 : a = byzantine â†’ b â‰¤ byzantine â†’ byzantine â‰¤ b)
 : a â‰¤ b := by
  cases a <;> cases b <;> try decide
  repeat (simp at c1 c2)

@[simp] theorem meet_false : â‹€ P f = false â†” âˆƒ x âˆˆ P, f x = false := by
  unfold bigAnd;
  have h : P.fold min true f â‰¤ false â†” _ âˆ¨ âˆƒ x âˆˆ P, f x â‰¤ false :=
    Finset.fold_min_le false
  simpa using h

@[simp] theorem meet_byzantine : â‹€ P f = byzantine â†” (âˆ€ x âˆˆ P, byzantine â‰¤ f x) âˆ§ âˆƒ x âˆˆ P, f x = byzantine := by
  unfold bigAnd;
  have h1 : P.fold min true f â‰¤ byzantine â†” _ âˆ¨ âˆƒ x âˆˆ P, f x â‰¤ byzantine :=
    Finset.fold_min_le byzantine
  have h2 : byzantine â‰¤ P.fold min true f â†” _ âˆ§ âˆ€ x âˆˆ P, byzantine â‰¤ f x :=
    Finset.le_fold_min byzantine
  generalize P.fold Atom.and true f = y at *
  constructor
  intro x; rw [x] at h1 h2; simp at h1 h2; 
  constructor; assumption; rcases h1 with âŸ¨p1, p2, p3âŸ©; exists p1; constructor; assumption
  apply le_antisymm; assumption; apply h2; assumption
  rintro âŸ¨a, bâŸ©; apply le_antisymm; apply h1.mpr; simp; rcases b with âŸ¨p1, p2, p3âŸ©;
  exists p1; constructor; assumption; exact ge_of_eq p3.symm
  apply h2.mpr; simp; assumption

@[simp] theorem meet_true : â‹€ P f = true â†” âˆ€ x âˆˆ P, f x = true := by
  unfold bigAnd;
  have h : true â‰¤ P.fold min true f â†” _ âˆ§ âˆ€ x âˆˆ P, true â‰¤ f x :=
    Finset.le_fold_min true
  simpa using h

@[simp] theorem join_false : â‹ P f = false â†” âˆ€ x âˆˆ P, f x = false := by
  unfold bigOr;
  have h : P.fold max false f â‰¤ false â†” _ âˆ§ âˆ€ x âˆˆ P, f x â‰¤ false :=
    Finset.fold_max_le false
  simpa using h

theorem join_le_byzantine : â‹ P f â‰¤ byzantine â†” (âˆ€ x âˆˆ P, f x â‰¤ byzantine) := by
  have h1 : P.fold max false f â‰¤ byzantine â†” _ âˆ§ âˆ€ x âˆˆ P, f x â‰¤ byzantine :=
    Finset.fold_max_le byzantine
  simpa using h1

theorem byzantine_le_meet : byzantine â‰¤ â‹€ P f â†” âˆ€ x âˆˆ P, f x â‰¥ byzantine := by
  have h2 : byzantine â‰¤ P.fold min true f â†” _ âˆ§ âˆ€ x âˆˆ P, byzantine â‰¤ f x :=
    Finset.le_fold_min (f := f) byzantine
  simpa using h2

theorem byzantine_le_join : byzantine â‰¤ â‹ P f â†” âˆƒ x âˆˆ P, f x â‰¥ byzantine := by
  have h2 : byzantine â‰¤ P.fold max false f â†” _ âˆ¨ âˆƒ x âˆˆ P, f x â‰¥ byzantine :=
    Finset.le_fold_max byzantine
  simpa using h2

theorem le_meet : a â‰¤ â‹€ P f â†” âˆ€ x âˆˆ P, a â‰¤ f x := by
  simpa using (Finset.le_fold_min (b := true) a)

theorem meet_le : â‹€ P f â‰¤ a â†” a = true âˆ¨ âˆƒ x âˆˆ P, f x â‰¤ a := by
  simpa using (Finset.fold_min_le (b := true) a)

theorem le_join : a â‰¤ â‹ P f â†” a = false âˆ¨ âˆƒ x âˆˆ P, f x â‰¥ a := by
  simpa using (Finset.le_fold_max (b := false) a)

theorem join_le : â‹ P f â‰¤ a â†” âˆ€ x âˆˆ P, f x â‰¤ a := by
  simpa using (Finset.fold_max_le (b := false) a)

theorem join_byzantine : â‹ P f = byzantine â†” (âˆ€ x âˆˆ P, f x â‰¤ byzantine) âˆ§ âˆƒ x âˆˆ P, f x = byzantine := by
  unfold bigOr;
  have h1 : P.fold max false f â‰¤ byzantine â†” _ âˆ§ âˆ€ x âˆˆ P, f x â‰¤ byzantine :=
    Finset.fold_max_le byzantine
  have h2 : byzantine â‰¤ P.fold max false f â†” _ âˆ¨ âˆƒ x âˆˆ P, f x â‰¥ byzantine :=
    Finset.le_fold_max byzantine
  simp at h2 h1
  generalize P.fold Atom.or false f = y at *
  constructor
  rintro âŸ¨_âŸ©; constructor; simpa using h1; simp at h1 h2
  rcases h2 with âŸ¨u, mu, puâŸ©; exists u; exists mu; exact (h1 u mu).antisymm pu
  rintro âŸ¨l, âŸ¨r, mr, prâŸ©âŸ©; have p1 := h1.mpr l; have p2 := h2.mpr âŸ¨r, mr, ge_of_eq prâŸ©
  exact p1.antisymm p2

@[simp] theorem join_true : â‹ P f = true â†” âˆƒ x âˆˆ P, f x = true := by
  unfold bigOr;
  have h : true â‰¤ P.fold max false f â†” _ âˆ¨ âˆƒ x âˆˆ P, true â‰¤ f x :=
    Finset.le_fold_max true
  simpa using h

theorem meet_neg : â‹€ P (Â¬ f) = Â¬ â‹ P f := by
  have := Finset.fold_hom (op := Atom.or) (op' := Atom.and) (b := false) (f := f) (m := Atom.neg) (s := P) ?_
  simp at this; exact this; apply neg_or

theorem join_neg : â‹ P (Â¬ f) = Â¬ â‹€ P f := by
  have := Finset.fold_hom (op := Atom.and) (op' := Atom.or) (b := true) (f := f) (m := Atom.neg) (s := P) ?_
  simp at this; exact this; apply neg_and

theorem le_implies_valid (p : a â‰¤ b) : âŠ¨ a â†’ âŠ¨ b := by
  intro x; cases a <;> cases b <;> cases x <;> simp at *

@[simp] theorem TF_true_eval : TF true = true := by rfl
@[simp] theorem TF_false_eval : TF false = true := by rfl
@[simp] theorem TF_byzantine_eval : TF byzantine = false := by rfl

@[simp] theorem T_true_eval : T true = true := by rfl
@[simp] theorem T_false_eval : T false = false := by rfl
@[simp] theorem T_byzantine_eval : T byzantine = false := by rfl

theorem valid_TF : âŠ¨ (TF a) â†” a = true âˆ¨ a = false := by
  constructor <;> intro h <;> cases a <;> cases h <;> first | contradiction | simp

@[simp] theorem valid_T : âŠ¨ (T a) â†” a = true := by
  constructor <;> intro h <;> cases a <;> cases h <;> simp

theorem valid_cases : âŠ¨ a â†” a = true âˆ¨ a = byzantine := by cases a <;> simp

theorem valid_byzantine_le : âŠ¨ a â†” byzantine â‰¤ a := by cases a <;> simp

@[simp] theorem byzantine_le_T : .byzantine â‰¤ T a â†” a = true := by cases a <;> simp
end Lemmas

end Three
